if(build_module_user_versions.isModule) {
    apply plugin: 'com.android.library'
}else{
    apply plugin: 'com.android.application'
}

android {
    compileSdkVersion build_module_user_versions.compile_sdk
    buildToolsVersion build_module_user_versions.build_tools
    defaultConfig {
        if(!build_module_user_versions.isModule) {
            applicationId build_module_user_versions.application_id
        }
        minSdkVersion build_module_user_versions.min_sdk
        targetSdkVersion build_module_user_versions.target_sdk
        versionCode build_module_user_versions.code
        versionName build_module_user_versions.name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName(), AROUTER_GENERATE_DOC: "enable"]
            }
        }

        resourcePrefix "module_user_"

    }

    sourceSets {
        main {
            if (!build_module_user_versions.isModule) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //release模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.support.v4
    implementation deps.support.design
    implementation deps.support.annotations
    implementation deps.support.recyclerview
    implementation deps.support.cardview
    implementation deps.support.core_utils
    implementation deps.support.appcompat

    implementation deps.constraint_layout

    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    annotationProcessor deps.dagger.compiler
    annotationProcessor deps.dagger.android_support_compiler

    implementation deps.arouter.runtime
    annotationProcessor deps.arouter.compiler

    implementation deps.rx.rxjava2
    implementation deps.rx.rx_android

    implementation deps.butterknife.runtime
    annotationProcessor deps.butterknife.compiler

    testImplementation deps.junit
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    implementation project(path: ':libres')
    implementation project(path: ':libdata')
    implementation project(path: ':libbase')
    implementation project(path: ':libprovider')
}
